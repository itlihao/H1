ext.mainApp = true  //设置为true，表示此module为主app module，一直以application方式编译
apply from: rootProject.file('cc-settings.gradle')

android {
    compileSdkVersion rootProject.compileVersion
    aaptOptions {
        noCompress 'dat'
    }

    defaultConfig {
        applicationId "com.hospital.s1m"
        minSdkVersion rootProject.minVersion
        targetSdkVersion rootProject.targetVersion
        versionCode 18
        versionName "1.0.1"

        ndk {
            abiFilters 'armeabi-v7a', "x86", "x86_64"
        }

        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "4d44b63825062cea20ac2d76", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]

//        resValue("string", "growingio_project_id", "babc6aee5f877686")
//        resValue("string", "growingio_url_scheme", "growing.626e7692cc914359")
    }

    signingConfigs {

        def Properties localProps = new Properties()
        localProps.load(new FileInputStream(file('../gradle.properties')))
        def Properties keyProps = new Properties()

        // 如果读取不到'keystore.props.file'属性，就使用debug keystore

        if (localProps['keystore.props.file']) {
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
        } else {
            keyProps["store"] = '../key/debug.keystore'
            keyProps["alias"] = 'androiddebugkey'
            keyProps["storePass"] = 'android'
            keyProps["pass"] = 'android'
        }
        config {
            // release版本使用assert确保存在该属性否则报错，避免错误打包
            keyAlias keyProps["alias"]
            keyPassword keyProps["pass"]
            storePassword keyProps["storePass"]
            storeFile file(keyProps["store"])
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':lib_base')
    implementation project(':lib_user')
    implementation project(':lib_ime')
    implementation project(':lib_print')
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.5'
    //极光推送
    implementation 'cn.jiguang.sdk:jpush:3.1.6'
    // 此处以JPush 3.1.5 版本为例。
    implementation 'cn.jiguang.sdk:jcore:1.2.5'
    implementation 'com.growingio.android:vds-android-agent:2.4.5@aar'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
}
